#depend "chem"
#depend "Box2dWeb-2.1.a.3" bare

const b2Vec2 = Box2D.Common.Math.b2Vec2
const b2BodyDef = Box2D.Dynamics.b2BodyDef
const b2Body = Box2D.Dynamics.b2Body
const b2FixtureDef = Box2D.Dynamics.b2FixtureDef
const b2World = Box2D.Dynamics.b2World
const b2PolygonShape = Box2D.Collision.Shapes.b2PolygonShape
const b2CircleShape = Box2D.Collision.Shapes.b2CircleShape

Chem.onReady ->
  const canvas = document.getElementById "game"
  const engine = new Chem.Engine canvas
  const batch = new Chem.Batch!
  const boom = new Chem.Sound 'sfx/boom.ogg'
  const ship = new Chem.Sprite 'ship',
    batch: batch
    pos: new Chem.Vec2d(200, 200)
    rotation: Math.PI / 2

  # GIMME THE FIZZAQS!
  world = null
  ship_body = null
  let
    gravity = new b2Vec2 0, 20
    world := new b2World gravity, true

    fixDef = new b2FixtureDef!
    fixDef.density = 1.0
    fixDef.friction = 0.5
    fixDef.restitution = 0

    bodyDef = new b2BodyDef!

    # create ground
    bodyDef.type = b2Body.b2_staticBody
    bodyDef.position.x = 9
    bodyDef.position.y = 13
    fixDef.shape = new b2PolygonShape!
    fixDef.shape.SetAsBox 10, 0.5
    world.CreateBody(bodyDef).CreateFixture fixDef

    # create some objects
    bodyDef.type = b2Body.b2_dynamicBody
    fixDef.shape = new b2CircleShape 1
    bodyDef.position.x = 5
    bodyDef.position.y = 5
    ship_body := world.CreateBody bodyDef
    ship_body.CreateFixture fixDef

  engine.on 'update', (dt, dx) ->
    world.Step dt, 1, 1
    world.ClearForces!

    ship.pos = new Chem.Vec2d ship_body.m_xf.position
    ship.pos.scale 30

    # JOMP!
    if engine.buttonJustPressed Chem.Button.Key_Up
      ship_body.ApplyForce new b2Vec2(0, -2000), ship_body.m_xf.position

    # press space to blow yourself up
    if engine.buttonJustPressed Chem.Button.Key_Space
      boom.play!
      ship.setAnimationName 'boom'
      ship.setFrameIndex 0
      ship.on 'animation_end', ->
        ship.delete!

  engine.on 'draw', (context) ->
    # clear canvas to black
    context.fillStyle = '#000000'
    context.fillRect 0, 0, engine.size.x, engine.size.y

    # draw all sprites in batch
    engine.draw batch

    # draw a little fps counter in the corner
    context.fillStyle = '#ffffff'
    engine.drawFps!

  engine.start!
  canvas.focus!

